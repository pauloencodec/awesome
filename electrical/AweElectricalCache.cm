/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 LinkÃ¶ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome.electrical;

public class AweElectricalCache {
    
    public constructor() {

    }

    private str->AweElectricalInfeedPoint infeeds() : public readable;
    private str->AweElectricalCableLine cables() : public readable;

    extend public AweElectricalCableLine cable( AweElectricalCableLine cable ) {
        cables.put( cable.id, cable );
        return cable;
    }

    extend public AweElectricalCableLine cable(str id, point start, point end, bool clickable=true, double weight=1.0, bool clickableP0=true, bool clickableP1=true) {
        var cable = cables.get(id);
        if(!cable) {
            cable = this.createCableInstance();
            cable.id = id;
            cables.put(id, cable);
        }

        cable.clickable = clickable;
        cable.clickableP0 = clickableP0;
        cable.clickableP1 = clickableP1;
        cable.snapperLine.p0 = start;
        cable.snapperLine.p1 = end;
        cable.weight = weight;
        
        return cable;
    }

    extend public AweElectricalCableLine createCableInstance() {
        return new AweElectricalCableLine(line());
    }

    extend public AweElectricalInfeedPoint infeed(str id, point position, orientation orientation, AweElectricalCableLine[] cables=null, AweObject[] customConfigurators=null) {
        var infeed = infeeds.get(id);
        if(!infeed) {
            infeed = this.createInfeedInstance();
            infeed.id = id;
            infeeds.put(id, infeed);
        }
        infeed.position = position;
        set*(infeed: orientation, cables);
        infeed.customConfigurators = customConfigurators;
        return infeed;
    }

    extend public AweElectricalInfeedPoint createInfeedInstance() {
        return new AweElectricalInfeedPoint(point());
    }

    extend public Primitive3D get3D() {
        if (animation()) return null;
        if (this.infeeds.empty() and this.cables.empty()) return null;

        Primitive3D[] prims();

        for (key, cable in this.cables) {
            if (!cable.active) continue;

            prims << this.getPoweredCable3D(cable);
        }

        var result = prims.safeSet3D();
        
        result.setMaterial(plainGreenMaterial3D);
        result.layer = #electrical;

        return result;
    }

    extend private Primitive3D getPoweredCable3D(AweElectricalCableLine cable) {
        Awe3D prims();

        var lineThickness = 0.3inch;
        var flowline = cable.snapper.toSnapper(cable.flow);

        if (flowline.length > 5inch) {
            prims << new Arrow3D(flowline.p0, flowline.p1, 0.7inch, lineThickness, 1.5inch);
        }
        else {
            prims << new Cylinder3D(flowline.p0, flowline.p1, lineThickness);
        }

        return prims;
    }
}