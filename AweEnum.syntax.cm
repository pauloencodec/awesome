/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 Link√∂ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome;

use cm.syntax;

public definition @visibility aweEnum @name=id @args=actualArgList ';' {
    SMembers members(noSrcRef);
    SStatements entriesInit();

    SId backingEntries("_entries");

    members << member {
        @visibility AweEnumBase[] @backingEntries;
    };
    
    for (i in (0..(args.count - 1))) {
        SActualArg arg = args[i];
        SExpr value = arg.e();
        str fieldName = arg.keyword();

        members << member {
            extend @visibility @name @fieldName() {
                return new @name(@value);
            }
        };

        entriesInit << statement {
            this.@backingEntries << this.@fieldName();
        };
    }

    members << member {
        @visibility AweEnumBase[] entries() {
            if (this.@backingEntries) {
                return this.@backingEntries;
            }

            this.@backingEntries = new AweEnumBase[]();

            @entriesInit;

            return this.@backingEntries;
        }
    };

    str singletonName = name.name;
    SType enumType(name.name);

    definition {
        @visibility class @name extends AweEnum {
            @members;
        }

        @visibility @enumType @singletonName = new @enumType();
    };
}