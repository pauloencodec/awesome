/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 Link√∂ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome.schemes;
use cm.syntax;

public syntax <SId,SId,SId> aweSchemeBinding {
    @propName=id "=" @schemeKey=id "." @schemePropKey=id {
        <SId,SId,SId> bindingInfo = <propName, schemeKey, schemePropKey>;
        return bindingInfo;
    }
}

public member @visibility aweSchemeBindings '(' @bindings=[aweSchemeBinding ',']* @lastBinding=aweSchemeBinding ')' ';' {
    <SId, SId, SId>[] allBindings();

    for (b in bindings) {
        allBindings << b;
    }

    allBindings << lastBinding;

    SStatements ifs();

    for (binding in allBindings) {
        SId propName = binding.v0;
        str propNameStr = binding.v0.name;
        SId schemeKey = binding.v1;
        SId schemePropKey = binding.v2;
        str schemePropKeyStr = binding.v2.name;
        str schemeKeyStr = schemeKey.name;

        ifs << statement {
            if (applier.is(@schemeKey)) {
                if (applier.propEnabled(@schemePropKeyStr)) {
                    if (schemeModel as @schemeKey) {
                        var visible = this.getVisible(@propNameStr);
                        
                        if (visible) {
                            var domain = this.getDomain(@propNameStr);
                            var value = schemeModel.@schemePropKey;

                            if (!domain or value in domain) {
                                this.@propName = schemeModel.@schemePropKey;
        
                                applied = true;
                            }
                        }
                    }
                }
            }
        };
    }

    return member {
        @visibility bool applyScheme(AweSchemeApplier applier) {
            bool applied = super(..);

            var schemeModel = applier.schemeModel;

            @ifs;

            return applied;
        }
    };
}