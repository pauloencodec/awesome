/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 LinkÃ¶ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome.connectors;

public class AweMagnet {
    public constructor(str key, bool positive) {
        this.key = key;
        this.positive=positive;
    }

    public str key;
    public bool positive;

    extend public bool attracts(AweMagnet other) {
        return this.key == other.key and this.positive != other.positive;
    }

    public Object copy(CopyEnv env, bool shallow=false) {
        return this;
    }
}

public bool magnetSetsAttract(AweMagnet{} oneSet, AweMagnet{} otherSet) {
    for(magnet in oneSet) {
        for(otherMagnet in otherSet) {
            if(magnet.attracts(otherMagnet)) {
                return true;
            }
        }
    }
    return false;
}

public str->AweMagnet magnetCache();

public AweMagnet createMagnet(str key, bool charge) {
    var cacheKey = key # charge;
    var magnet = magnetCache.get(cacheKey);

    if (!magnet) {
        magnet = new AweMagnet(key, charge);

        magnetCache.put(cacheKey, magnet);
    }

    return magnet;
}

public expr magnet '(' @key=id @charge=magnetCharge ')'  {
    str keyStr = key.name;
    return blockExpr {
        result createMagnet(@keyStr, @charge.v);
    };
}

public syntax Bool magnetCharge {
    '+' { return Bool(true); }
    '-' { return Bool(false); }
    empty { return null; }
}

{
    var m = magnet(structure+);
    pln(#m.positive; #m.key);

    var other = magnet(structure+);
    pln(#m.attracts(other));
}