/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 Link√∂ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome;

public class AweEnum extends AweEnumBase : abstract {
    private ObjectSubSet _domain;

    public ObjectSubSet domain(function(Object item):bool filter=null, AweEnum[] includes=null, AweEnum[] excludes=null) {
        if (!this._domain) {
            this._domain = new ObjectSubSet(this.entries().:Object[]);
        }

        if (filter) {
            return this.filterDomain(filter);
        }

        if (includes and includes.any()) {
            return this.includeInDomain(includes);
        }

        if (excludes and excludes.any()) {
            return this.excludeFromDomain(excludes);
        }

        return this._domain;
    }

    extend private ObjectSubSet filterDomain(function(Object item):bool filter) {
        Object[] filtered();

        for (item in this._domain._members) {
            if (filter(item)) {
                filtered << item;
            }
        }

        return new ObjectSubSet(filtered);
    }

    extend private ObjectSubSet includeInDomain(AweEnum[] includes) {
        Object[] filtered();

        for (item in this._domain._members) {
            if (item.AweEnum.in(includes)) {
                filtered << item;
            }
        }

        return new ObjectSubSet(filtered);
    }

    extend private ObjectSubSet excludeFromDomain(AweEnum[] excludes) {
        Object[] filtered();

        for (item in this._domain._members) {
            if (!item.AweEnum.in(excludes)) {
                filtered << item;
            }
        }

        return new ObjectSubSet(filtered);
    }

    extend public bool in(rest: AweEnum[] args) {
        return this.in(args);
    }

    extend public bool in(AweEnum[] args) {
        for (item in args) {
            if (item == this) {
                return true;
            }
        }

        return false;
    }
    
    public bool equal(Object other, EqualEnv env=EqualEnv()) {
        if (other as AweEnum) {
            return this.class == other.class and this.name == other.name and this.key == other.key;
        }

        return super(..);
    }

    extend public bool operator==(Object other) {
        return this.equal(other);
    }
}

public class AweEnumBase : abstract {
    public str key;
    public str name;
    public str value;
    public Object data;

    public constructor() {
    }

    public constructor(str key, str name, str value) {
        this.key = key;
        this.name = name;
        this.value = value;
    }

    public constructor(<str, str> entry, str name) {
        this(entry.v0, name, entry.v1);
    }

    public constructor(<str, str, Object> entry, str name) {
        this(entry.v0, name, entry.v1);

        this.data = entry.v2;
    }

    extend public AweEnum[] entries() : abstract {}
    extend public ObjectSubSet domain(function(Object item):bool filter=null, AweEnum[] includes=null, AweEnum[] excludes=null) : abstract {}

    public aweDynamicContract {
        aweDynamic str toStr(str extraInfo);
    }

    extend public str toStr(str extraInfo) {
        if (extraInfo == "value") return this.value;
        if (extraInfo == "name") return this.name;

        return this.key;
    }

    public str toS() {
        return this.value;
    }
}

//Example of usage
// public aweEnum GrommetLocation (
//     center = <"GC", "Grommet Center">,
//     both = <"GB", "LH and RH">
// );

// public class Table {
//     public GrommetLocation grommetLocation;

//     public constructor(GrommetLocation grommetLocation) {
//         this.grommetLocation = grommetLocation; 
//     }
// }

// public aweObject TableModel {
//     props (
//         GrommetLocation grommetLocation = GrommetLocation.center
//     );

//     public aweObjectDomain (
//         GrommetLocation grommetLocation = GrommetLocation.domain()
//     );
// }